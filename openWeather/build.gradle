apply plugin: 'com.android.application'
//apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.aware.plugin.openweather"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode version_code
        versionName version_readable
        multiDexEnabled true
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = android.defaultConfig.applicationId
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/denzilferreira/com.aware.plugin.openweather'
        issueTrackerUrl = 'https://github.com/denzilferreira/com.aware.plugin.openweather/issues'
        labels = ['aar','android','framework','aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/denzilferreira/com.aware.plugin.openweather.git'
        desc = "AWARE Plugin: Open Weather"
        userOrg = user
        githubRepo = "denzilferreira/com.aware.plugin.openweather"

        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

repositories {
    maven { url "https://jitpack.io" } //MPAndroidChart
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:multidex:1.0.0'
    compile "com.android.support:appcompat-v7:${support_libs}"
    compile "com.awareframework:aware-core:${aware_libs}@aar"
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile 'com.koushikdutta.ion:ion:2.1.6'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
    compile 'com.google.android.gms:play-services:8.4.0'
}
